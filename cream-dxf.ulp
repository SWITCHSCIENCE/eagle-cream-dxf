//
// Export the EAGLE cream layers to DXF for the Craft-ROBO cutting machine.
//
// Copyright (c) 2010 Switch Science, Inc.
//

real SHRINK_WIDTH = 0.15;		// unit: mm

string HEADER =
"  0\n"
"SECTION\n"
"  2\n"
"HEADER\n"
"  9\n"
"$ACADVER\n"
"  1\n"
"AC1013\n"
"  9\n"
"$MEASUREMENT\n"
" 70\n"
"     1\n"				// unit: mm
"  0\n"
"ENDSEC\n"
"  0\n"
"SECTION\n"
"  2\n"
"ENTITIES\n";

string POLYLINE =
"  0\n"
"LWPOLYLINE\n"
"  8\n"
"0\n"					// layer
" 62\n"
"7\n"					// color
" 90\n"
"%d\n"					// number of points
" 70\n"
"0\n";					// open polyline

string POLYLINE_POINT =
" 10\n"
"%f\n"
" 20\n"
"%f\n";

string LINE =
"  0\n"
"LINE\n"
"  8\n"
"0\n"					// layer
" 10\n"
"%f\n"					// start x
" 20\n"
"%f\n"					// start y
" 11\n"
"%f\n"					// end x
" 21\n"
"%f\n";					// end y

string ARC =
"  0\n"
"ARC\n"
"  8\n"
"0\n"					// layer
" 10\n"
"%f\n"					// center x
" 20\n"
"%f\n"					// center y
" 40\n"
"%f\n"					// radius
" 50\n"
"%f\n"					// start angle
" 51"
"%f\n";					// end angle

string CIRCLE =
"  0\n"
"CIRCLE\n"
"  8\n"
"0\n"					// layer
" 10\n"
"%f\n"					// center x
" 20\n"
"%f\n"					// center y
" 40\n"
"%f\n";					// radius

string TRAILER =
"  0\n"
"ENDSEC\n"
"  0\n"
"EOF\n";

void processLayer(UL_BOARD B, int layer) {
  int cream = (layer == LAYER_TOP ? LAYER_TCREAM : LAYER_BCREAM);
  printf("%s", HEADER);
  B.elements(E) {
    E.package.contacts(C) {
      if (C.smd && C.smd.layer == layer) {
	real x = C.smd.x / 10000.0;
	real y = C.smd.y / 10000.0;
	real w = C.smd.dx[cream] / 10000.0 / 2 - SHRINK_WIDTH;
	real h = C.smd.dy[cream] / 10000.0 / 2 - SHRINK_WIDTH;
	real r = C.smd.roundness;
	real a = C.smd.angle / 180 * PI;
	real wc = w * cos(a);
	real hs = h * sin(a);
	real ws = w * sin(a);
	real hc = h * cos(a);
	printf(POLYLINE, 6);	// 6 points
	printf(POLYLINE_POINT, x + wc - hs, y + ws + hc);	// a
	printf(POLYLINE_POINT, x - wc - hs, y - ws + hc);	// b
	printf(POLYLINE_POINT, x - wc + hs, y - ws - hc);	// c
	printf(POLYLINE_POINT, x + wc + hs, y + ws - hc);	// d
	printf(POLYLINE_POINT, x + wc - hs, y + ws + hc);	// a
	printf(POLYLINE_POINT, x - wc - hs, y - ws + hc);	// b
      }
    }
  }
  printf("%s", TRAILER);
}

board(B) {
  output(filesetext(B.name, "-tcream.dxf")) processLayer(B, LAYER_TOP);
  output(filesetext(B.name, "-bcream.dxf")) processLayer(B, LAYER_BOTTOM);
}
